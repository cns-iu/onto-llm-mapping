PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX uberon: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX description1: <http://purl.obolibrary.org/obo/IAO_0000115>
PREFIX description2: <http://purl.obolibrary.org/obo/UBPROP_0000010>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX body: <http://purl.obolibrary.org/obo/UBERON_0013702>

#SELECT (COUNT(DISTINCT ?term) as ?count)
SELECT DISTINCT (?term as ?iri) ?id ?name ?aka ?description
FROM <https://purl.humanatlas.io/collection/hra>
FROM <https://purl.humanatlas.io/collection/hra/redundant>
WHERE {
  VALUES (?input) {
    (body:)
  }
  ?term part_of:* ?input .
  FILTER(STRSTARTS(STR(?term), STR(uberon:)))

  ?term rdfs:label ?name .

  OPTIONAL { 
    SELECT ?term (GROUP_CONCAT(DISTINCT ?akaLabel; SEPARATOR='; ') as ?aka)
    WHERE {
      ?term oboInOwl:hasExactSynonym ?akaLabel .
      FILTER(STRSTARTS(STR(?term), STR(uberon:)))
    }
    GROUP BY ?term
  }

  OPTIONAL { ?term description1: ?description1 . }
  OPTIONAL { ?term description2: ?description2 . }
  BIND(IF(BOUND(?description1), ?description1, IF(BOUND(?description2), ?description2, ?name)) as ?description)
  BIND(REPLACE(STR(?term), STR(uberon:), 'UBERON_') as ?id)
}
ORDER BY ?id
